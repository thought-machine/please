def exists(file:str):
    path = join_path(package_name(), file)
    return f"echo -n '{path} exists: '; if [ -f '{path}' ]; then echo OK; else echo FAIL; exit 1; fi"

def not_exists(file:str):
    path = join_path(package_name(), file)
    return f"echo -n '{path} does not exist: '; if [ -f '{path}' ]; then echo FAIL; exit 1; else echo OK; fi"

def runtime_dep(name:str):
    return genrule(
        name = name,
        outs = [name],
        cmd = "touch $OUTS",
    )

def target(name:str, build_tests:list=[], post_build:function=None, requires:list=None, provides:dict=None,
           runtime_deps:list=[], deps:list=[]):
    # Ensure that run-time dependencies are not present at build time, unless they are also
    # explicitly given as build-time dependencies. (The lstrips here turn build target names
    # into the names of files they output - they're slightly grungy, but they allow us to
    # re-use exists and not_exists outside of this function; they work because targets
    # generated by runtime_dep output a single file who name is identical to that of the
    # target's.)
    cmd = [not_exists(r.lstrip(":")) for r in runtime_deps if r not in deps]
    cmd += [exists(d.lstrip(":")) for d in deps]
    return genrule(
        name = name,
        outs = [name],
        # Assume that if a post-build function is defined, it dynamically adds a run-time
        # dependency, which requires this target to be marked as binary.
        binary = len(runtime_deps) > 0 or post_build is not None,
        cmd = cmd + build_tests + ["touch $OUTS"],
        post_build = post_build,
        requires = requires,
        provides = provides,
        runtime_deps = runtime_deps,
        deps = deps,
    )


target(
    name = "target_with_no_runtime_deps",
)

target(
    name = "target_with_runtime_deps",
    build_tests = [
        # Ensure that the run-time dependencies :target_with_post_build_runtime_dep and
        # :target_requiring_kittens are not present at build time (these ones are tricky
        # to identify statically, hence explicitly listing them here).
        not_exists("post_build_runtime_dep"),
        not_exists("provides_runtime_dep"),
    ],
    runtime_deps = [
        ":runtime_dep",
        ":target_with_another_runtime_dep",
        ":target_with_build_and_runtime_deps",
        ":target_with_post_build_runtime_dep",
        ":target_requiring_kittens",
    ],
)

target(
    name = "target_with_another_runtime_dep",
    runtime_deps = [":another_runtime_dep"],
)

target(
    name = "target_with_build_and_runtime_deps",
    runtime_deps = [":build_and_runtime_dep"],
    deps = [":build_and_runtime_dep"],
)

target(
    name = "target_with_post_build_runtime_dep",
    build_tests = [
        # Ensure that the run-time dependency added by the post-build function is not present at
        # build time (this one can't be identified statically, hence explicitly listing it here).
        not_exists("post_build_runtime_dep"),
    ],
    post_build = lambda name, _: add_runtime_dep(name, ":post_build_runtime_dep"),
)

target(
    name = "src_with_runtime_dep",
    runtime_deps = [":src_runtime_dep"],
    deps = [":src_dep_with_runtime_dep"],
)

target(
    name = "src_dep_with_runtime_dep",
    runtime_deps = [":src_dep_runtime_dep"],
)

target(
    name = "dep_with_runtime_dep",
    runtime_deps = [":dep_runtime_dep"],
)

target(
    name = "target_requiring_kittens",
    build_tests = [
        # Ensure that neither of the run-time dependencies that could possibly be provided by
        # :runtime_dep_providing_kittens or :target_with_provides_runtime_dep are present at
        # build time (only the latter *should* be provided, but it isn't possible to generate
        # both of these tests statically, hence explicitly listing them here).
        not_exists("runtime_dep_providing_kittens_runtime_dep"),
        not_exists("provides_runtime_dep"),
    ],
    requires = ["kittens"],
    runtime_deps = [":runtime_dep_providing_kittens"],
)

target(
    name = "runtime_dep_providing_kittens",
    provides = {
        "kittens": ":target_with_provides_runtime_dep",
    },
    runtime_deps = [":runtime_dep_providing_kittens_runtime_dep"],
)

target(
    name = "target_with_provides_runtime_dep",
    runtime_deps = [":provides_runtime_dep"],
)

target(
    name = "test_data_with_runtime_dep",
    runtime_deps = [":test_data_runtime_dep"],
)

target(
    name = "test_tool_with_runtime_dep",
    runtime_deps = [":test_tool_runtime_dep"],
)

runtime_dep("runtime_dep")
runtime_dep("another_runtime_dep")
runtime_dep("build_and_runtime_dep")
runtime_dep("post_build_runtime_dep")
runtime_dep("src_runtime_dep")
runtime_dep("src_dep_runtime_dep")
runtime_dep("dep_runtime_dep")
runtime_dep("test_data_runtime_dep")
runtime_dep("test_tool_runtime_dep")
runtime_dep("provides_runtime_dep")
runtime_dep("runtime_dep_providing_kittens_runtime_dep")

gentest(
    name = "runtime_deps_test_case",
    srcs = {
        "src_with_runtime_dep": [":src_with_runtime_dep"],
    },
    outs = ["runtime_deps_test_case"],
    cmd = [
        # Ensure this target's sources' and dependencies' run-time dependencies are present at build time...
        exists("src_runtime_dep"),
        exists("dep_runtime_dep"),
        # ...but that those targets' (build-time) dependencies' run-time dependencies are not...
        not_exists("src_dep_runtime_dep"),
        # ...and that this target's run-time dependencies are not...
        not_exists("runtime_dep"),
        not_exists("another_runtime_dep"),
        not_exists("build_and_runtime_dep"),
        not_exists("post_build_runtime_dep"),
        not_exists("provides_runtime_dep"),
        not_exists("runtime_dep_providing_kittens_runtime_dep"),
        # ...and that this target's test-time dependencies are not.
        not_exists("test_data_runtime_dep"),
        not_exists("test_tool_runtime_dep"),
        "touch $OUTS",
    ],
    data = [":test_data_with_runtime_dep"],
    no_test_output = True,
    runtime_deps = [":target_with_runtime_deps"],
    test_cmd = [
        # Ensure this target's sources' and dependencies' (transitive) run-time dependencies are not present at test time...
        not_exists("src_runtime_dep"),
        not_exists("dep_runtime_dep"),
        not_exists("src_dep_runtime_dep"),
        # ...but that this target's run-time dependencies are...
        exists("runtime_dep"),
        exists("another_runtime_dep"),
        exists("build_and_runtime_dep"),
        exists("post_build_runtime_dep"),
        exists("provides_runtime_dep"),
        # ...and that this target's test-time dependencies are...
        exists("test_data_runtime_dep"),
        exists("test_tool_runtime_dep"),
        # ...and that the requires/provides in the run-time dependency tree were correctly resolved.
        not_exists("runtime_dep_providing_kittens_runtime_dep"),
    ],
    test_tools = [":test_tool_with_runtime_dep"],
    deps = [
        ":dep_with_runtime_dep",
    ],
)
