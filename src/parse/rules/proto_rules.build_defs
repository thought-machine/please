"""Build rules for compiling protocol buffers & gRPC service stubs.

Note that these are some of the most complex of our built-in build rules,
because of their cross-language nature. Each proto_library rule declares a set of
sub-rules to run protoc & the appropriate java_library, go_library rules etc. Users
shouldn't worry about those sub-rules and just declare a dependency directly on
the proto_library rule to get its appropriate outputs.

It is possible to add extra languages to these for generation. This is accomplished
via the 'languages' argument; this can be simply a list of languages to build, but
can also be a mapping of language name -> definition of how to build it. The definition
should be the return value of proto_language.
"""

_DEFAULT_GRPC_LABELS = ['grpc']


def proto_library(name, srcs, plugins=None, deps=None, visibility=None, labels=None,
                  python_deps=None, cc_deps=None, java_deps=None, go_deps=None,
                  languages=None, test_only=False, root_dir='',
                  protoc_flags=None):
    """Compile a .proto file to generated code for various languages.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies
      visibility (list): Visibility specification for the rule.
      labels (list): List of labels to apply to this rule.
      languages (list | dict): List of languages to generate rules for, chosen from the set {cc, py, go, java, js}.
                               Alternatively, a dict mapping the language name to a definition of how to build it
                               (see proto_language for more details of the values).
      test_only (bool): If True, can only be used in test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      python_deps (list): Deprecated, replaced by languages.
      java_deps (list): Deprecated, replaced by languages.
      go_deps (list): Deprecated, replaced by languages.
      cc_deps (list): Deprecated, replaced by languages.
      plugins (dict): Deprecated, replaced by languages.
    """
    languages = _merge_dicts(languages or CONFIG.PROTO_LANGUAGES, proto_languages())
    labels = labels or []
    deps = deps or []

    # We detect output names for normal sources, but will have to do a post-build rule for
    # any input rules. We could just do that for everything but it's nicer to avoid them
    # when possible since they obscure what's going on with the build graph.
    file_srcs = [src for src in srcs if src[0] not in {':', '/'}]
    need_post_build = file_srcs != srcs
    provides = {'proto': ':_%s#proto' % name}

    for language, plugin in sorted(languages.items()):
        outs = [src.replace('.proto', ext) for src in file_srcs for ext in plugin['extensions']] if plugin['use_file_names'] else []
        cmd = '$TOOL1 --%s_out=$TMP_DIR %s %s' % (plugin['language'], ' '.join(plugin['protoc_flags']), ' '.join(protoc_flags or []))
        if root_dir:
            cmd = 'export RD="%s"; cd $RD; %s ${SRCS//$RD\\//} && cd $TMP_DIR' % (root_dir, cmd.replace('$TMP_DIR', '.'))
        else:
            cmd += ' ${SRCS}'
        cmds = [cmd, 'mv -f ${PKG}/* .']

        # TODO(pebers): genericise these bits?
        if language == 'py' and CONFIG.PROTO_PYTHON_PACKAGE:
            cmds.append('find . -name "*_pb2.py" | xargs sed -i -e "s/from google.protobuf/from %s/g"' %
                        CONFIG.PROTO_PYTHON_PACKAGE)
        elif language == 'go':
            base_path = get_base_path()
            labels += ['proto:go-map: %s/%s=%s/%s' % (base_path, src, base_path, name) for src in srcs
                       if not src.startswith(':') and not src.startswith('/')]

        post_build = None
        if need_post_build or not plugin['use_file_names']:
            cmds.append('find . %s | sort' % ' -or '.join('-name "*%s"' % ext for ext in plugin['extensions']))
            post_build = _annotate_outs(plugin['extensions'])

        protoc_rule = build_rule(
            name = name,
            tag = 'protoc_' + language,
            srcs = srcs,
            outs = outs,
            cmd = ' && '.join(cmds),
            deps = deps,
            tools = [CONFIG.PROTOC_TOOL] + plugin['tools'],
            requires = ['proto'],
            pre_build = plugin['pre_build'],
            post_build = post_build,
            labels = labels,
            needs_transitive_deps = True,
            test_only = test_only,
            visibility = visibility,
        )

        lang_name = '_%s#%s' % (name, language)
        provides[language] = plugin['func'](lang_name, [protoc_rule], deps + plugin['deps'], test_only) or (':' + lang_name)

    # This simply collects the sources, it's used for other proto_library rules to depend on.
    filegroup(
        name = '_%s#proto' % name,
        srcs = srcs,
        visibility = visibility,
        exported_deps = deps,
        labels = labels,
        requires = ['proto'],
        output_is_complete = False,
        test_only = test_only,
    )
    # This is the final rule that directs dependencies to the appropriate language.
    filegroup(
        name = name,
        deps = sorted(provides.values()),
        provides = provides,
        visibility = visibility,
        labels = labels,
        test_only = test_only,
    )


def grpc_library(name, srcs, deps=None, visibility=None, languages=None, python_deps=None,
                 java_deps=None, go_deps=None, cc_deps=None, labels=None, test_only=False,
                 root_dir='', protoc_flags=None):
    """Defines a rule for a grpc library.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies (other grpc_library or proto_library rules)
      visibility (list): Visibility specification for the rule.
      languages (list | dict): List of languages to generate rules for, chosen from the set {cc, py, go, java}.
                               Alternatively, a dict mapping the language name to a definition of how to build it
                               (see proto_language for more details of the values).
      labels (list): List of labels to apply to this rule.
      test_only (bool): If True, this rule can only be used by test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      python_deps (list): Deprecated, replaced by languages.
      java_deps (list): Deprecated, replaced by languages.
      go_deps (list): Deprecated, replaced by languages.
      cc_deps (list): Deprecated, replaced by languages.
    """
    proto_library(
        name = name,
        srcs = srcs,
        deps = deps,
        languages = _merge_dicts(languages or CONFIG.PROTO_LANGUAGES, grpc_languages()),
        visibility = visibility,
        labels = (labels or []) + _DEFAULT_GRPC_LABELS,
        test_only = test_only,
        root_dir = root_dir,
        protoc_flags = protoc_flags,
    )


def _go_path_mapping(grpc):
    """Used to update the Go path mapping; by default it doesn't really import in the way we want."""
    grpc_plugin = 'plugins=grpc,' if grpc else ''
    def _map_go_paths(rule_name):
        mapping = ',M'.join(get_labels(rule_name, 'proto:go-map:'))
        cmd = get_command(rule_name)
        new_cmd = cmd.replace('--go_out=', '--go_out=%sM%s:' % (grpc_plugin, mapping))
        set_command(rule_name, new_cmd)
    return _map_go_paths


def proto_language(language, extensions, func, use_file_names=True, protoc_flags=None,
                   tools=None, deps=None, pre_build=None):
    """Returns the definition of how to build a particular language for proto_library.

    Args:
      language (str): Name of the language (as protoc would name it).
      extensions (list): File extensions that will get generated.
      func (function): Function defining how to build the rule. It will receive the following arguments:
            name: Suggested name of the rule.
            srcs: Source files, as generated by protoc.
            deps: Suggested dependencies.
            test_only: True if the original rule was marked as test_only.
            It should return the name of any rule that it wants added to the final list of provides.
      use_file_names (bool): True if the output file names are normally predictable.
            This is the case for most languages but not e.g. Java where they depend on the
            declarations in the proto file. If False we'll attempt to detect them.
      protoc_flags (list): Additional flags for the protoc invocation for this rule.
      tools (list): Additional tools to apply to this rule.
      deps (list): Additional dependencies to apply to this rule.
      pre_build (function): Definition of pre-build function to apply to this language.
    """
    return {
        'language': language,
        'extensions': extensions,
        'func': func,
        'use_file_names': use_file_names,
        'protoc_flags': protoc_flags or [],
        'tools': tools or [],
        'deps': deps or [],
        'pre_build': pre_build,
    }


def _parent_rule(name):
    """Returns the parent rule, i.e. strips the leading _ and trailing #hashtag."""
    return name[:name.find('#')].lstrip('_')


def _annotate_outs(extensions):
    """Used to collect output files when we can't determine them without running the rule.

    For Java this is always the case because their location depends on the java_package option
    defined in the .proto file. For other languages we might not know if the sources come from
    another rule.
    """
    def _annotate_outs(rule_name, output):
        for out in output:
            for ext in extensions:
                if out.endswith(ext):
                    add_out(rule_name, out.lstrip('./'))
    return _annotate_outs


def _merge_dicts(a, b):
    """Merges dictionary a into dictionary b, overwriting where a's values are not None."""
    if not isinstance(a, dict):
        # Special case, we generate headers separately.
        # TODO(pebers): A more elegant approach would be to be able to have some form of
        #               grouped or named outputs, so we can feed one rule into cc_library and somehow
        #               separate it into headers and sources.
        if 'cc' in a and 'cc_hdrs' not in a:
            a.append('cc_hdrs')
        return {x: b[x] for x in a}  # Languages can be passed as just a list.
    if 'cc' in a and 'cc_hdrs' not in a:
        a['cc_hdrs'] = None
    return {k: v or b[k] for k, v in a.items()}


def proto_languages():
    """Returns the known set of proto language definitions.

    Due to technical reasons this can't just be a global (if you must know: the lambdas need
    to bind to the set of globals for the BUILD file, not the set when we load the rules).
    TODO(pebers): This seems a bit ugly and might be slow if we're creating a lot of temporaries.
                  Find a way to persist these...
    """
    return {
        'cc': proto_language(
            language = 'cpp',
            extensions = ['.pb.cc'],
            func = lambda name, srcs, deps, test_only: cc_library(
                name = name,
                srcs = srcs,
                hdrs = [srcs[0] + '_hdrs'],
                deps = deps,
                test_only = test_only,
                pkg_config_libs = ['protobuf'],
                compiler_flags = ['-I$PKG'],
            ),
        ),
        'cc_hdrs': proto_language(
            language = 'cpp',
            extensions = ['.pb.h'],
            func = lambda name, srcs, deps, test_only: ':' + name,
        ),
        'java': proto_language(
            language = 'java',
            extensions = ['.java'],
            use_file_names = False,
            func = lambda name, srcs, deps, test_only: java_library(
                name = name,
                srcs = srcs,
                exported_deps = deps,
                test_only = test_only,
            ),
            deps = [CONFIG.PROTO_JAVA_DEP],
        ),
        'go': proto_language(
            language = 'go',
            extensions = ['.pb.go'],
            func = lambda name, srcs, deps, test_only: go_library(
                name = name,
                srcs = srcs,
                out = _parent_rule(name) + '.a',
                deps = deps,
                test_only = test_only,
            ),
            protoc_flags = ['--plugin=protoc-gen-go=$TOOL2'],
            tools = [CONFIG.PROTOC_GO_PLUGIN],
            deps = [CONFIG.PROTO_GO_DEP],
            pre_build = _go_path_mapping(False),
        ),
        'js': proto_language(
            language = 'js',
            extensions = ['_pb.js'],
            func = lambda name, srcs, deps, test_only: filegroup(
                name = name,
                srcs = srcs,
                deps = deps,
                test_only = test_only,
                requires = ['js'],
                output_is_complete = False,
            ),
            protoc_flags = ['--js_out=import_style=commonjs,binary:$TMP_DIR'],
            deps = [CONFIG.PROTO_JS_DEP],
        ),
        'py': proto_language(
            language = 'python',
            extensions = ['_pb2.py'],
            func = python_library,
            deps = [CONFIG.PROTO_PYTHON_DEP],
        ),
    }


def grpc_languages():
    """Returns the predefined set of gRPC languages."""
    return {
        'cc': proto_language(
            language = 'cpp',
            extensions = ['.pb.cc', '.grpc.pb.cc'],
            func = lambda name, srcs, deps, test_only: cc_library(
                name = name,
                srcs = srcs,
                hdrs = [srcs[0] + '_hdrs'],
                deps = deps,
                test_only = test_only,
                pkg_config_libs = ['grpc++', 'grpc', 'protobuf'],
                compiler_flags = ['-I$PKG', '-Wno-unused-parameter'],  # Generated gRPC code is not robust to this.
            ),
            protoc_flags = ['--plugin=protoc-gen-grpc-cc=$TOOL2', '--grpc-cc_out=$TMP_DIR'],
            tools = [CONFIG.GRPC_CC_PLUGIN],
        ),
        'cc_hdrs': proto_language(
            language = 'cpp',
            extensions = ['.pb.h', '.grpc.pb.h'],
            func = lambda name, srcs, deps, test_only: ':' + name,
            protoc_flags = ['--plugin=protoc-gen-grpc-cc=$TOOL2', '--grpc-cc_out=$TMP_DIR'],
            tools = [CONFIG.GRPC_CC_PLUGIN],
        ),
        'py': proto_language(
            language = 'python',
            extensions = ['_pb2.py'],
            func = python_library,
            protoc_flags = ['--plugin=protoc-gen-grpc-python=$TOOL2', '--grpc-python_out=$TMP_DIR'],
            tools = [CONFIG.GRPC_PYTHON_PLUGIN],
            deps = [CONFIG.PROTO_PYTHON_DEP, CONFIG.GRPC_PYTHON_DEP],
        ),
        'java': proto_language(
            language = 'java',
            extensions = ['.java'],
            use_file_names = False,
            func = lambda name, srcs, deps, test_only: java_library(
                name = name,
                srcs = srcs,
                exported_deps = deps,
                test_only = test_only,
            ),
            protoc_flags = ['--plugin=protoc-gen-grpc-java=$TOOL2', '--grpc-java_out=$TMP_DIR'],
            tools = [CONFIG.GRPC_JAVA_PLUGIN],
            deps = [CONFIG.GRPC_JAVA_DEP, CONFIG.PROTO_JAVA_DEP],
        ),
        'go': proto_language(
            language = 'go',
            extensions = ['.pb.go'],
            func = lambda name, srcs, deps, test_only: go_library(
                name = name,
                srcs = srcs,
                out = _parent_rule(name) + '.a',
                deps = deps,
                test_only = test_only,
            ),
            protoc_flags = ['--plugin=protoc-gen-go=$TOOL2'],
            tools = [CONFIG.PROTOC_GO_PLUGIN],
            deps = [CONFIG.PROTO_GO_DEP, CONFIG.GRPC_GO_DEP],
            pre_build = _go_path_mapping(True),
        ),
        # We don't really support grpc-js right now, so this is the same as proto-js.
        'js': proto_language(
            language = 'js',
            extensions = ['_pb.js'],
            func = lambda name, srcs, deps, test_only: filegroup(
                name = name,
                srcs = srcs,
                deps = deps,
                test_only = test_only,
                requires = ['js'],
                output_is_complete = False,
            ),
            protoc_flags = ['--js_out=import_style=commonjs,binary:$TMP_DIR'],
            deps = [CONFIG.PROTO_JS_DEP],
        ),
    }
